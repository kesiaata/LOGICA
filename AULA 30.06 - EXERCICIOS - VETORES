Exercícios: Vetores

Crie os algoritmos para os itens a seguir, testando os programas no Portugol Studio.

1. Ler 10 números e imprimi-los na ordem inversa da leitura, ou seja, o último número lido deve ser o primeiro impresso.

2. Cadastrar 10 números de matrículas de alunos e armazená-los em um vetor. Esses números são distintos, ou seja, não podem existir números de matrículas iguais, e deve estar no intervalo de 100 a 200.
Mostrar na tela as matrículas armazenadas, em ordem crescente.

3. Crie um vetor de 5 números informados pelo usuário e monte outro vetor contendo os valores do primeiro multiplicados por 3. 
Mostrar os elementos de ambos os vetores na tela, lado a lado.
Número	x3
1	3
2	6
4	12
5	15
3	9

4. Cadastrar 5 nomes de professores e seus respectivos tempos de profissão. Armazene esses valores em dois vetores: vetProfessor e vetTempoCasa.
Mostrar os dados na tela, em uma tabela com os nomes dos professores na coluna à esquerda, e os tempos de profissão em uma coluna à direita.
Professor	Tempo de Casa (anos)
João		10
Maria		15
Ana		2
Renata		8
Jorge		22

5. Um depósito armazena 8 produtos distintos, e para cada tipo de produto existe um código. Crie um algoritmo para ler o código do produto e a 
quantidade em estoque. Monte dois vetores para armazenar respectivamente os códigos das mercadorias e a quantidade dos produtos.
Mostrar na tela os dados, em formato tabular.
CodProduto	QtdeEstoque
TV-01		9
AS-03		7
RF-14		3




                                                                RESOLVIDOS

programa
{
//1. Ler 10 números e imprimi-los na ordem inversa da leitura, ou seja, o último número lido deve ser o primeiro impresso.
	inteiro num[10], i
	
	
	funcao inicio()
	{
		escreva("Digite 10 numeros:\n")
		para(i = 0; i <= 9; i++){
			leia(num[i])
			
		}
		para(i = 9; i >= 0; i--){
			escreva("numero: "+ num[i]+ "\n")
		}
	}
}



programa
{
//2. Cadastrar 10 números de matrículas de alunos e armazená-los em um vetor. Esses números são distintos, ou seja, não podem existir números de 
//matrículas iguais, e deve estar no intervalo de 100 a 200. Mostrar na tela as matrículas armazenadas, em ordem crescente.
	
	
inteiro alunos[10]
	inteiro mat, pos, cont, ordeA, ordeB, troque = 0
	caracter controle = 's'
	funcao inicio()
	{
		para(pos = 0; pos < 10; pos++){
			enquanto(verdadeiro){
				escreva("Digite a matrícula: ")
				leia(mat)	
				
				//Verificar se matrícula já existe
				para(cont = 0; cont < 10; cont++){
					se(mat == alunos[cont]){
						escreva("Valor já existe! Digite outro\n")
						controle = 'n'
					}
				}
				
				//Verificar se números estão no intervalo válido
				se((mat < 100) ou (mat > 200)){
					escreva("Valor inválido!\n")
					controle = 'n'
				}
	
				//Se matrícula válida, efetiva-la
				se(controle != 'n'){
					alunos[pos] = mat
					pare
				}

				//Libera cadastro de dados no vetor
				controle = 's'
			}
		}

		//Ordenar valores e exibi-los na tela:
		para(ordeA = 0; ordeA < 10; ordeA++){
			para(ordeB = ordeA+1; ordeB < 10; ordeB++){
				se(alunos[ordeA] > alunos[ordeB]){
					troque = alunos[ordeB]
					alunos[ordeB] = alunos[ordeA]
					alunos[ordeA] = troque
				}
			}
		}
		para(ordeA = 0; ordeA < 10; ordeA++)
		escreva(alunos[ordeA]+ " ")	
	}
}




programa {
//3. Crie um vetor de 5 números informados pelo usuário e monte outro vetor contendo os valores do primeiro 
//multiplicados por 3. Mostrar os elementos de ambos os vetores na tela, lado a lado.
//Número	x3
//1	3
//2	6
//4	12
//5	15
//3	9

        inteiro num[5], i , mult[5]
        
	funcao inicio() {
	   
	    para(i = 0; i <= 4; i++){
	        escreva("Digite um número:\n")
	        leia(num[i])
	        mult[i] = num[i] * 3
	    }
	    escreva("Número  x3 ")
	    para(i = 0; i <= 4; i++){
	        escreva("\n"+ num[i]+ " "+ mult[i]+ "\n")
	    }
		
	}
}




programa {
//4. Cadastrar 5 nomes de professores e seus respectivos tempos de profissão. Armazene esses valores em dois 
//vetores: vetProfessor e vetTempoCasa. Mostrar os dados na tela, em uma tabela com os nomes dos 
//professores na coluna à esquerda, e os tempos de profissão em uma coluna à direita.
//Professor	Tempo de Casa (anos)
//João		10
//Maria		15
//Ana		2
//Renata	8
//Jorge		22

    inteiro vetTempoCasa[5], i
    cadeia vetProfessor[5]
    
	funcao inicio() {
	    para(i = 0; i <= 4; i++){
	        escreva("Digite seu Nome: ")
	        leia(vetProfessor[i])
	        escreva("Escreva seu Tempo de Profissão: ")
	        leia(vetTempoCasa[i])
	    }
	    escreva("\nProfessor Tempo de casa(anos)\n")
	    para(i = 0; i <= 4; i++){
	    escreva("\n"+ vetProfessor[i]+ "     "+ vetTempoCasa[i])
	    }
		
	}
}




programa {
//5. Um depósito armazena 8 produtos distintos, e para cada tipo de produto existe um código. 
//Crie um algoritmo para ler o código do produto e a quantidade em estoque. Monte dois vetores para 
//armazenar respectivamente os códigos das mercadorias e a quantidade dos produtos. Mostrar na tela os dados, 
//em formato tabular.
//CodProduto	QtdeEstoque
//TV-01		9
//AS-03		7
//RF-14		3

    inteiro qtdeEstoque[8], i
    cadeia codProduto[8]

	funcao inicio() {
	    para(i = 0; i <= 7; i++){
	        escreva("Digite o Código do Produto: ")
	        leia(codProduto[i])
	        escreva("Digite a Quantidade em Estoque: ")
	        leia(qtdeEstoque[i])
	    }
	    escreva("\nCodProduto	QtdeEstoque\n")
	    para(i = 0; i <= 7; i++){
	    escreva("\n"+ codProduto[i]+ "     "+ qtdeEstoque[i])
	    }
		
	}
}

